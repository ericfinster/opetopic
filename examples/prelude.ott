--
--  Prelude
--

module Prelude where {

  idp (X : Cat) (x : |X|) : Cell X [ box x (nd (dot x) lf) ] = refl x ;

  vcomp (X : Cat) (x : |X|) (y : |X|) (z : |X|) 
        (f : Cell X [ box y (nd (dot x) lf) ]) 
        (g : Cell X [ box z (nd (dot y) lf) ]) 
    : Cell X [ box z (nd (dot x) lf) ] 
    = comp (nd g (nd (nd f (nd lf lf)) lf)) ;

  vfill (X : Cat) (x : |X|) (y : |X|) (z : |X|)
  	(f : Cell X [ box y (nd (dot x) lf) ])
  	(g : Cell X [ box z (nd (dot y) lf) ])
    : Cell X [ box z (nd (box y (nd (dot x) lf)) lf)
             | box (comp (nd g (nd (nd f (nd lf lf)) lf)))
  	         (nd (dot g) (nd (nd (dot f) (nd lf lf)) lf)) ]
    = fill (nd g (nd (nd f (nd lf lf)) lf)) ;
    
  unit-right (X : Cat) (x : |X|) (y : |X|) (f : Cell X [ box y (nd (dot x) lf) ])  
    : Cell X [ box y (nd (dot x) lf) 
             | box (comp (nd f (nd (nd (refl x) (nd lf lf)) lf))) (nd (dot f) (nd lf lf)) ] 
    = comp (nd (fill (nd f (nd (nd (refl x) (nd lf lf)) lf))) (nd lf (nd (nd (nd (drop x) lf) (nd lf lf)) lf))) ;

  unit-left (X : Cat) (x : |X|) (y : |X|) (f : Cell X [ box y (nd (dot x) lf) ])  
    : Cell X [ box y (nd (dot x) lf) 
             | box (comp (nd (refl y) (nd (nd f (nd lf lf)) lf))) (nd (dot f) (nd lf lf)) ] 
    = comp (nd (fill (nd (refl y) (nd (nd f (nd lf lf)) lf))) (nd (nd (drop y) lf) (nd (nd lf (nd lf lf)) lf))) ;

  whisker-right (X : Cat) (x : |X|) (y : |X|) (z : |X|) 
                (f : Cell X [ box y (nd (dot x) lf) ]) 
                (g : Cell X [ box y (nd (dot x) lf) ]) 
                (h : Cell X [ box z (nd (dot y) lf) ]) 
                (a : Cell X [ box y (nd (dot x) lf) | box g (nd (dot f) (nd lf lf)) ])  
    : Cell X [ box z (nd (dot x) lf) 
             | box (comp (nd h (nd (nd g (nd lf lf)) lf))) 
                 (nd (dot (comp (nd h (nd (nd f (nd lf lf)) lf))))
		   (nd lf lf)) ] 
    = liftTgt (fill (nd h (nd (nd f (nd lf lf)) lf))) 
        (fillIsTgt (nd h (nd (nd f (nd lf lf)) lf))) 
          (comp (nd h (nd (nd g (nd lf lf)) lf))) 
          (comp (nd (fill (nd h (nd (nd g (nd lf lf)) lf))) 
            (nd lf (nd (nd (nd a (nd lf (nd lf lf))) (nd lf lf)) lf)))) ;

  whisker-left (X : Cat) (x : |X|) (y : |X|) (z : |X|) 
               (f : Cell X [ box y (nd (dot x) lf) ]) 
               (g : Cell X [ box z (nd (dot y) lf) ]) 
               (h : Cell X [ box z (nd (dot y) lf) ]) 
               (a : Cell X [ box z (nd (dot y) lf) | box h (nd (dot g) (nd lf lf)) ]) 
    : Cell X [ box z (nd (dot x) lf) 
             | box (comp (nd h (nd (nd f (nd lf lf)) lf))) 
                 (nd (dot (comp (nd g (nd (nd f (nd lf lf)) lf)))) (nd lf lf)) ] 
    = liftTgt (fill (nd g (nd (nd f (nd lf lf)) lf))) 
                (fillIsTgt (nd g (nd (nd f (nd lf lf)) lf))) 
                  (comp (nd h (nd (nd f (nd lf lf)) lf))) 
                  (comp (nd (fill (nd h (nd (nd f (nd lf lf)) lf))) 
                    (nd (nd a (nd lf (nd lf lf))) (nd (nd lf (nd lf lf)) lf)))) ;

  hcomp (X : Cat) (x : |X|) (y : |X|) (z : |X|) 
        (f : Cell X [ box y (nd (dot x) lf) ]) (g : Cell X [ box z (nd (dot y) lf) ]) 
        (h : Cell X [ box y (nd (dot x) lf) ]) (k : Cell X [ box z (nd (dot y) lf) ]) 
        (a : Cell X [ box y (nd (dot x) lf) | box h (nd (dot f) (nd lf lf)) ]) 
        (b : Cell X [ box z (nd (dot y) lf) | box k (nd (dot g) (nd lf lf)) ]) 
    : Cell X [ box z (nd (dot x) lf) 
             | box (comp (nd k (nd (nd h (nd lf lf)) lf))) 
                 (nd (dot (comp (nd g (nd (nd f (nd lf lf)) lf)))) 
                   (nd lf lf)) ]
    = liftTgt (fill (nd g (nd (nd f (nd lf lf)) lf))) 
        (fillIsTgt (nd g (nd (nd f (nd lf lf)) lf))) 
          (comp (nd k (nd (nd h (nd lf lf)) lf))) 
          (comp (nd (fill (nd k (nd (nd h (nd lf lf)) lf))) 
            (nd (nd b (nd lf (nd lf lf))) (nd (nd (nd a (nd lf (nd lf lf))) (nd lf lf)) lf)))) ;

  has-left-ext (X : Cat) (x : |X|) (y : |X|)
               (f : Cell X [ box y (nd (dot x) lf) ]) : U
    = (g : Cell X [ box x (nd (dot y) lf) ]) * isSrcUniv (vfill X x y x f g) {} ;
	       
  biclosed (X : Cat) : U
    = (x : |X|) (y : |X|) (f : Cell X [ box y (nd (dot x) lf) ]) ->
        has-left-ext X x y f ;

}