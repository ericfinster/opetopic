--- Input file to BNF Converter
---   This describes the labelled BNF Grammar of
---   OpetopicTT concrete syntax

entrypoints Prog ;

-- Programs
Prog.     Prog   ::= [Decl] ;

-- Expressions

EPair.    Exp    ::= Exp1 "," Exp ;

ELam.     Exp1   ::= "\\" Patt "." Exp1 ;
EPi.      Exp1   ::= [PTele] "->" Exp1 ;
ESig.     Exp1   ::= [PTele] "*" Exp1 ;
EArrow.   Exp1   ::= Exp2 "->" Exp1 ;
ETimes.   Exp1   ::= Exp2 "*"  Exp1 ;

EObj.     Exp2   ::= "|" Exp3 "|" ;
ECell.    Exp2   ::= "Cell" Exp3 "[" [Nst] "]" ;

EApp.     Exp3   ::= Exp3 Exp4 ;

EIsTgtU.  Exp4   ::= "isTgtUniv" Exp4;
EIsSrcU.  Exp4   ::= "isSrcUniv" Exp4 Addr;

ERefl.    Exp4   ::= "refl" Exp4 ;
EDrop.    Exp4   ::= "drop" Exp4 ;
EComp.    Exp4   ::= "comp" Tree ;
EFill.    Exp4   ::= "fill" Tree ;

ELiftTgt.  Exp4   ::= "liftTgt" Exp4 Exp4 Exp4 Exp4;
ELiftSrc.  Exp4   ::= "liftSrc" Exp4 Exp4 Exp4 Exp4;
EFillTgt.  Exp4   ::= "fillTgt" Exp4 Exp4 Exp4 Exp4;
EFillSrc.  Exp4   ::= "fillSrc" Exp4 Exp4 Exp4 Exp4;

EDropIsTgt.     Exp4 ::= "dropIsTgt" Exp4;
EFillIsTgt.     Exp4 ::= "fillIsTgt" Tree;
EShellIsTgt.    Exp4 ::= "shellIsTgt" Exp4 Exp4 Tree Exp4;
EFillTgtIsTgt.  Exp4 ::= "fillTgtTgt" Exp4 Exp4 Exp4 Exp4;
EFillSrcIsTgt.  Exp4 ::= "fillSrcTgt" Exp4 Exp4 Exp4 Exp4;
EFillTgtIsSrc.  Exp4 ::= "fillTgtSrc" Exp4 Exp4 Exp4 Exp4;
EFillSrcIsSrc.  Exp4 ::= "fillSrcSrc" Exp4 Exp4 Exp4 Exp4;

EUnit.    Exp5   ::= "Unit";
ECat.     Exp5   ::= "Cat";
ETt.      Exp5   ::= "tt";
EFst.     Exp5   ::= Exp5 ".1" ;
ESnd.     Exp5   ::= Exp5 ".2" ;
EType.    Exp5   ::= "U" ;
EVar.     Exp5   ::= Ident ;

coercions Exp 5 ;

-- Telescopes
Tele.     Tele ::= "(" Patt ":" Exp ")" ;
terminator Tele "" ;

-- Trees
TLf.      Tree ::= "lf" ;
TNd.      Tree ::= "nd" TVal Tree ;
_.        Tree ::= "(" Tree ")" ;

VNst.     TVal ::= Nst ;
VTree.    TVal ::= Tree ;
VExp.     TVal ::= Exp5 ;

-- Nestings
TDot.     Nst ::= "dot" Exp4 ;
TBox.     Nst ::= "box" Exp4 Tree ;
_.        Nst ::= "(" Nst ")" ;
separator Nst "||" ;

-- Addresses
TAddr.    Addr ::= "{" [Addr] "}";
separator Addr "|";

-- Nonempty telescopes with Exp:s, this is hack to avoid ambiguities
-- in the grammar when parsing Pi
PTele.    PTele ::= "(" Patt ":" Exp ")" ;
terminator nonempty PTele "" ;

-- declarations
Def.      Decl ::= "let" Ident [Tele] ":" Exp1 "=" Exp1 ;
separator Decl ";" ;

-- patterns
PUnit.    Patt   ::= "_" ;
PVar.     Patt   ::= Ident ;

comment "--" ;
comment "{-" "-}" ;
